module Prelude where

data Bool : Type 0 where
  True 
  False

log not : Bool -> Bool
not = \ b . 
  case b [_] of 
     True -> False
     False -> True

log and : Bool -> Bool -> Bool
and = \ b1 b2. 
  case b1 [_] of 
    True -> b2
    False -> False

log or : Bool -> Bool -> Bool
or = \b1 b2. 
  case b1 [_] of 
    True -> True
    False -> b2


log bool_eq : (Bool @ log) -> (Bool @ log) -> Bool
bool_eq = \ x y . 
  case x [x_eq], y [y_eq] of 
     True, True -> True
     False, False -> True
     _ , _ -> False


data Nat : Type 0 where
    Zero
    Succ of (x : Nat)

pred : Nat -> Nat
pred = \n . case n [_] of
  Zero -> Zero
  Succ n' -> n'

log nat_eq : (Nat @ log) -> (Nat @ log) -> Bool
ind nat_eq x = \ y . 
  case x [x_eq], y [y_eq] of 
     Zero, Zero -> True
     Succ m, Succ n -> nat_eq m [ord x_eq] n
     _ , _ -> False
     


-- S m = S n -> m = n
--
-- Poor man's inversion.
--
-- This strategy provides inversion for any trellys types: join and a
-- projection function (here pred).
sm_eq_Sn__m_eq_n : (m:Nat) -> (n:Nat) -> (Succ m = Succ n) -> m = n
sm_eq_Sn__m_eq_n = \m n sm_eq_Sn .
  let pSm_eq_m [_]   = (join : pred (Succ m) = m) in
  let pSn_eq_n [_]   = (join : pred (Succ n) = n) in
  _

-- I'm not sure, but I think generally we can make all
-- proof arguments run-time (since we don't plan to
-- actually run proofs). So I changed some arguments
-- from compile-time to run-time. AS.

-- But this is inconvenient when proving equalities.  You want the
-- arguments to these lemmas to be erased, because you want to use
-- eraseable things in them.  CJC

-- Do we have an example where we care that the arguments are erasable?
-- My intution is that these proofs should only be used in two contexts:
-- as the argument to conv (where they are erased), or as lemmas to build
-- other proofs (so in erasable positions, so the arguments can be runtime). VS

-- SCW: I don't think that we care whether the arguments to sym/trans
-- etc are compiletime or runtime. All equality proofs are irrelevant
-- as the definition of uneraseEq below shows. (i.e. even if you use trans'
-- to produce an equality proof, you can still erase it with uneraseEq
-- below.) So I think we should just use runtime only arguments for
-- simplicity.


sym : (A : Type 0) -> (B : Type 0) -> (a : A@prog) -> (b : B@prog)  -> (p : a = b) -> (b = a)
sym = \ A B a b p . _

sym1 : (A : Type 1) -> (B : Type 1) -> (a : A@prog) -> (b : B@prog)  -> (p : a = b) -> (b = a)
sym1 = \ A B a b p . _

trans : [A : Type 0] -> [B : Type 0] -> [C : Type 0] ->
        [a : A@prog] -> [b : B@prog] -> [c : C@prog] ->
				[p : a = b] -> [q : b = c] -> (a = c)
trans = \ [A] [B] [C] [a] [b] [c] [p] [q] . _


trans1 : (A : Type 1) -> (B : Type 1) -> (C : Type 1) ->
         (a : A@prog) -> (b : B@prog) -> (c : C@prog) ->
				(p : a = b) -> (q1 : b = c)  -> (a = c)
trans1 = \ A B C a b c p q1. _

trans' : (A : Type 0) -> (B : Type 0) -> (C : Type 0) ->
         (a : A@prog) -> (b : B@prog) -> (c : C@prog) ->
				(p : a = b) -> (q : b = c) -> (a = c)
trans' = \ A B C a b c p q . _


trans4' : (A : Type 0) -> (B : Type 0) -> (C : Type 0) -> (D : Type 0) -> 
         (a : A@prog) -> (b : B@prog) -> (c : C@prog) -> (d : D@prog) -> 
				(p : a = b) -> (q : b = c) -> (r : c = d) -> (a = d)
trans4' = \ A B cTy dTy a b c d p q r . 
		    trans' A cTy dTy a c d
             (trans' A B cTy a b c p q) r

-- [a = b] -> a = b
--
-- Irrelevance for equality types.
--
-- Promote erased equality to run-time equality.
--
-- Useful with erased equalities generated by case matches.

uneraseEq : [A:Type 0] -> [B:Type 0] -> [a:A@prog] -> [b:B@prog] -> [a = b] -> (a = b)
uneraseEq = \ [_][_][a][b][eq] . _

-- Existential types.

data Exists (a : Type 0) (p : ((x:a@log) -> Type 0)@log) : Type 0 where
  Ex of [x : a@log] (witness : p x @log)

-- Unit

data Unit : Type 0 where
  U

-- Void
--
-- DON'T USE THIS: you probably want (0=1) or (True=False) instead,
-- since contra handles those directly ...
data Void : Type 0 where {}

void_elim : [A : Type] -> Void -> A
void_elim = \[A] v . case v [_] of {}

log cast : [A : Type] -> [B : Type] -> [A = B] -> (A@log) -> (B@log)
cast [A] [B] [p] x = x

