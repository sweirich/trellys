-- Sat solver example

-- This sat solver is not guaranteed to find a satisfying assignment, 
-- but if it does terminate with an assignment, that assignment must 
-- satisfy the formula.

-- The only prog function in the file is the main loop. All other helper
-- functions in this file are logical. It just happened that way---the helper
-- functions were easy to fit into the logical fragment.

-- This example also uses dependent types for bounds information. Variables are
-- represented as Fins. Formulas are indexed by the number of variables. An 
-- assignment is a vector with length equal to the number of free variables in 
-- the formula.

-- This file could also prove that partial_interp = Just True 
-- implies that interp = True

module Sat where

import Prelude
import List
import Product
import Fin
import Vectors
import Maybe

usually log

-- Literals are natural numbers paired with booleans
-- clauses are lists of literals
-- formulae are lists of clauses

log Formula : (n:Nat) -> Type 
Formula = \n. List (List (Times (Fin n) Bool))

-- an environment is a mapping from variables to truth values
log Env : (n:Nat) -> Type 
Env = \ n . Vector Bool n

log interp_lit : [n:Nat] => Vector Bool n -> (Times (Fin n) Bool @ log) -> Bool
interp_lit = \ [n] env lit . case lit of 
   Prod fn b -> bool_eq b (lookup [n] fn env)
     
log interp : [n:Nat] => Vector Bool n -> Formula n -> Bool
interp = \ [n] env formula . 
  unfold (Formula n) in
  all (any (interp_lit env)) (formula : List (List (Times (Fin n) Bool)))

-- general procedure for sat solving, i.e. showing that Phi is satisfiable
-- we want to search for a satisfying assignment to the literals in Phi

-- convert a partial assignment to an environment
-- (unassigned variables are just true)
log extend : [n:Nat] => Vector (Maybe Bool) n -> Vector Bool n
extend = \[n] xs . vmap (maybe True) [n] xs

log partial_interp_lit : [n:Nat] => (partial : Vector (Maybe Bool) n) 
              -> (lit : (Times (Fin n) Bool) @ log) -> Maybe Bool
partial_interp_lit = \[n] p lit .
   case lit of 
     Prod fn b -> case (lookup [n] fn p) of 
         Just v  -> Just (bool_eq b v)
         Nothing -> Nothing

-- is this literal unassigned by the map?
log unassigned_lit : [n : Nat] => (p : Vector (Maybe Bool) n) ->
       (c : (Times (Fin n) Bool) @ log) -> Bool
unassigned_lit = \[n] p lit . 
  (maybe_eq bool_eq) (partial_interp_lit p lit) Nothing

-- is this literal known to be true?
log true_lit : [n : Nat] => (p : Vector (Maybe Bool) n) ->
       (c : (Times (Fin n) Bool) @ log) -> Bool
true_lit = \[n] p lit . 
  (maybe_eq bool_eq) (partial_interp_lit p lit) (Just True)

-- is this literal known to be false?
log false_lit : [n : Nat] => (p : Vector (Maybe Bool) n) ->
       (c : (Times (Fin n) Bool) @ log) -> Bool
false_lit = \[n] p lit . 
  (maybe_eq bool_eq) (partial_interp_lit p lit) (Just False)

-- a clause is known to be true if any literal has been assigned true
-- or known to be false if all literals are assigned false
-- otherwise its truth value is unknown.
log partial_interp_clause : [n:Nat] => (partial : Vector (Maybe Bool) n) 
              -> (clause : List (Times (Fin n) Bool) @ log) -> Maybe Bool
partial_interp_clause [n] p c =
  if (any (true_lit p) c) 
    then Just True
    else if (all (false_lit p) c) 
         then Just False
         else Nothing

-- a formula is known to be true if all clauses are known to be true
-- and known to be false if any clauses are known to be false.
log satisfies : [n:Nat] => (partial : Vector (Maybe Bool) n) 
              -> (formula : Formula n) -> Maybe Bool
satisfies [n] p f =
   unfold (Formula n) in
   let f' = map (partial_interp_clause p) (f :  List (List (Times (Fin n) Bool))) in
   if (all (maybe_eq bool_eq (Just True)) f')
     then Just True
     else if (any (maybe_eq bool_eq (Just False)) f') 
          then Just False
          else Nothing

-- unit propagation
-- Find a clause with only one unassigned literal and no other true
-- literals. That one must be true.

log unit_lit_clause : [n : Nat] => (p : Vector (Maybe Bool) n) ->
       (c : List (Times (Fin n) Bool) @ log) -> List (Times (Fin n) Bool)
unit_lit_clause [n] p c = 
   let unassigned_lits = filter (unassigned_lit p) c in
	let true_lits = filter (true_lit p) c in
	if (and (nat_eq (length unassigned_lits) 1)
	          (nat_eq (length true_lits)       0)) 
	  then c 
	  else Nil

log unit_lits : [n : Nat] => (p : Vector (Maybe Bool) n) ->
       (formula : Formula n) -> List (Times (Fin n) Bool)
unit_lits = \ [n] p f . 
   unfold (Formula n) in
	concatMap (unit_lit_clause p) (f :  List (List (Times (Fin n) Bool)))

-- future work: pure literal assignment
-- If a propositional variable occurs with only one polarity in the
-- formula, it is called pure. Pure literals can always be assigned in
-- a way that makes all clauses containing them true. Thus, these
-- clauses do not constrain the search anymore and can be deleted. 

-- update the partial map validating the literals in the list
log set_lits : [n:Nat] => List (Times (Fin n) Bool) -> 
                  Vector (Maybe Bool) n -> Vector (Maybe Bool) n
set_lits = \[n] . ind set_lits l = \v.
  case l [l_eq] of 
    Nil -> v
    Cons x xs -> 
	    let v' = set_lits xs [ord l_eq] v in
		 set [n] (pi1 x) (Just (pi2 x) : Maybe Bool) v'


-- the result of sat solving, a satisfiable assignment
-- or "UNSAT"
data Answer (n : Nat) (formula : Formula n) : Type where
  SAT    of (assignment : Vector (Maybe Bool) n) 
            [_ : satisfies assignment formula = (Just True : Maybe Bool) ]
  UNSAT

-- find the index of the first unassigned variable in the partial map
log next_var : [n:Nat] -> Vector (Maybe Bool) n -> Maybe (Fin n)    
ind next_var [n] = \ v . 
  case v of 
    VNil [pf]  -> Nothing
    VCons [m][m_eq] x xs -> case x of 
       Just b  -> maybe_map (fin_succ [m][m_eq]) (next_var [m][ord m_eq] xs)
       Nothing -> Just (FZ [m][m_eq])

-- the simplest version goes through the literals one by one, trying assignments
-- until it finds a satisfying state for the given formula
-- prunes the search space when it is discovered to be UNSAT
-- looks for unit literals to propagate

prog dpll : [n:Nat] => (formula : Formula n) 
           -> Vector (Maybe Bool) n -> (Answer n formula @ L)
rec dpll [n] formula assign =
 case (satisfies assign formula) [s_eq] of 
   Just True  -> SAT assign [s_eq]
   Just False -> UNSAT
   Nothing -> 
     let units = unit_lits assign formula in
     if (not (null units))
        then dpll formula (set_lits units assign)
        else
          case (next_var [n] assign) of 
             Nothing -> UNSAT -- no more variables to try
             Just m -> 
				   let prog fc = (dpll formula
					            (set [n] m (Just False : Maybe Bool) assign)) in
               case fc of 
                 SAT assign [pf] -> SAT assign [pf]
                 UNSAT     ->
                    dpll formula 
                      (set [n] m (Just True : Maybe Bool) assign)

prog solver : (n:Nat) => (formula : Formula n) 
                      -> (Answer n formula @ L)
solver n formula = dpll formula (repeat (Nothing : Maybe Bool) n)

-- The termination of this algorithm decreases with the number of unassigned 
-- literals in the assignment. Perhaps not TOO difficult to show...

-- Would also be good to get some experience using HOFs in the programmatic 
-- fragment.

